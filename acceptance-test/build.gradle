plugins {
    id 'groovy'
    id 'java-gradle-plugin'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    pluginSourceSet parent.sourceSets.main
}

test {
    systemProperty 'current.gradle.version', gradle.gradleVersion
}

fileTree(projectDir) {
    include '*/build.gradle'
    include '*/*/build.gradle'
}.each { file ->
    final projectPath = relativePath(file.path) - '/build.gradle'
    final escapedProjectPath = projectPath.replace('/', '-')
    task("build-$escapedProjectPath", type: JavaExec, group: 'acceptance-test') {
        classpath = sourceSets.test.runtimeClasspath
        main = 'org.hidetake.gradle.swagger.generator.test.Main'
        args = ["$projectDir/$projectPath", '-s', 'build']
    }
    clean.dependsOn task("clean-$escapedProjectPath", type: Delete, group: 'clean') {
        delete "$projectDir/$projectPath/build"
    }
}

task('examples', description: 'Generate example documents', group: 'acceptance-test') {
    dependsOn 'build-ui-v3-basic'
    doLast {
        copy {
            from "$projectDir/ui-v3/basic/build/swagger-ui-petstore"
            into "$buildDir/examples/ui-v3"
        }
    }
    dependsOn 'build-redoc-basic'
    doLast {
        copy {
            from "$projectDir/redoc/basic/build/redoc-petstore"
            into "$buildDir/examples/redoc"
        }
    }
    dependsOn 'build-codegen-v3-html'
    doLast {
        copy {
            from "$projectDir/codegen-v3/html/build/swagger-code-petstore"
            into "$buildDir/examples/html"
        }
    }
}
